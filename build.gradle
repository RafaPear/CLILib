plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.1.20'
    id 'java-library'
    id 'maven-publish'
    id 'org.jetbrains.dokka' version '1.9.20'
}

group = 'pt.clilib'
version = '1.3-Beta'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    implementation("org.json:json:20231013")
    implementation("net.java.dev.jna:jna:5.17.0")           // JNA core
    implementation("net.java.dev.jna:jna-platform:5.17.0")  // funcionalidades extra (opcional mas recomendado)

}

test {
    useJUnitPlatform()
}
java {
    withSourcesJar()
    withJavadocJar()
}

def generatedVersionDir = "${buildDir}/generated-version"

sourceSets {
    main {
        java.srcDirs += 'src/main/java'
        output.dir(generatedVersionDir, builtBy: 'generateVersionProperties')
    }
    example {
        kotlin.srcDir("src/example/kotlin")
        resources.srcDir("src/example/resources")
        compileClasspath += sourceSets.main.output + configurations.runtimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}


task generateVersionProperties {
    doLast {
        def propertiesFile = file "$generatedVersionDir/version.properties"
        propertiesFile.parentFile.mkdirs()
        def properties = new Properties()
        properties.setProperty("version", rootProject.version.toString())
        propertiesFile.withWriter { properties.store(it, null) }
    }
}
processResources.dependsOn generateVersionProperties

tasks.named('javadocJar') {
    dependsOn tasks.named('dokkaJavadoc')
    from(tasks.named('dokkaJavadoc').get().outputDirectory)
}

tasks.named('jar') {
    dependsOn tasks.named('dokkaJavadoc')
    from(tasks.named('dokkaJavadoc').get().outputDirectory) {
        into('docs')
    }
}

kotlin {
    jvmToolchain(21)
}

tasks.register("runExample", JavaExec) {
    group = "application"
    description = "Runs the CLI example"
    classpath = sourceSets.example.runtimeClasspath
    mainClass.set("MainKt")
}

// Automatically publish the library to Maven Local after building so that
// projects depending on it can access the documentation and sources JARs.
tasks.named("build") {
    finalizedBy(tasks.named("publishToMavenLocal"))
}

publishing {
    publications {
        create("gpr", MavenPublication) {
            from(components.java)
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/RafaPear/CLILib")
            credentials {
                username = System.getenv("USERNAME") ?: findProperty("gpr.user")?.toString() ?: ""
                password = System.getenv("TOKEN") ?: findProperty("gpr.key")?.toString() ?: ""
            }
        }
    }
}
